:Class PrimeSieve
    :Field Private n
    :Field Public bits

    ⎕IO←0
    :Field Shared Private sieveSizes←10*1+⍳10
    :Field Shared Private sieveResults←4 25 168 1229 9592 78498 664579 5761455 50847534 455052511

    ∇ NewPrimeSieve limit
        :Implements Constructor
        :Access Public
        n←limit
    ∇

    ∇ runSieve
        :Access Public
        ⎕IO←0
        factor←1 ⍝ 3-2
        bits←n⍴0 1
        q←⌈n*0.5
        :While factor ≤ q
            factor⊢←2∘+⍣(bits⊃⍨⊣)factor
            :If factor > 50
                bits[factor×2+⍳2-⍨⌈n÷factor]←0
            :Else
                bits>←(n↑(2×factor)⍴1)<n⍴factor↑1
            :EndIf
        :EndWhile
    ∇

    ∇ r←countPrimes
        :Access Private
        r←+/bits
    ∇

    ∇ r←validateResults
        :Access Public
        ⎕IO←0
        resIndex←⍸n⍷sieveSizes
        :If resIndex≡⍬
            r←0
        :Else
        r←countPrimes=sieveResults[resIndex]
        :EndIf
    ∇

    ∇ printResults args
        :Access Public
        ⎕IO←0
        showResults←0⊃args
        duration←1⊃args
        passes←2⊃args
        :If showResults
            ⎕←2,1↓⍸bits
            ⍞←'Passes: ' ⋄ ⍞←passes ⋄ ⍞←', Time: ' ⋄ ⍞←duration
            ⍞←', Avg: ' ⋄ ⍞←duration÷passes ⋄ ⍞←', Limit: ' ⋄ ⍞←n
            ⍞←', Count: ' ⋄ ⍞←countPrimes ⋄ ⍞←', Valid: ' ⋄ ⍞←validateResults
            ⎕←''
        :EndIf
        ⍞←'garklein-tradfn;' ⋄ ⍞←passes ⋄ ⍞←';' ⋄ ⍞←duration ⋄ ⍞←';' ⋄ ⍞←'1;'
        ⍞←'algorithm=base,faithful=yes,bits=1'
    ∇
:EndClass
