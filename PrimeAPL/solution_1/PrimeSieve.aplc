:Class PrimeSieve
    :Field Private n
    :Field Private bits

    ⎕IO←0
    :Field Shared Public sieveSizes←10*1+⍳10
    :Field Shared Public sieveResults←4 25 168 1229 9592 78498 664579 5761455 50847534 455052511

    ∇ NewPrimeSieve limit
      :Implements Constructor
      :Access Public
      n←limit
    ∇

    ∇ r←runSieve
      :Access Public
      bits←runSieveDfn n
      r←1
    ∇
    runSieveDfn←{
        ⎕IO←0
        n←⍵
        factor←3
        bits←n⍴0 1
        q←⌈n*0.5
        _←{
            _←{
                factor>50:bits[factor×2+⍳2-⍨⌈n÷factor]←0
                1⊣bits>←(n↑(2×factor)⍴1)<n⍴factor↑1
            }0
            factor⊢←2∘+⍣(bits⊃⍨⊣)factor
            factor≤q:∇ 0
            1
        }0
        bits
    }

    ∇ r←countPrimes
      :Access Public
      r←countPrimesDfn bits
    ∇
    countPrimesDfn←+/

    ∇ r←validateResults
      :Access Private
      r←validateResultsDfn 0
    ∇
    validateResultsDfn←{
        ⎕IO←0
        resIndex←⍸n⍷sieveSizes
        resIndex≡⍬:0
        (countPrimesDfn bits)=sieveResults[resIndex]
    }

    ∇ r←printResults args
      :Access Public
      printResultsDfn args
      r←1
    ∇
    printResultsDfn←{
        showResults←1↑⍵
        duration←¯1↑2↑⍵
        passes←¯1↑⍵
        _←{
            showResults:showResultsDfn duration passes
            1
        }0
        ⍞←'garklein;' ⋄ ⍞←passes ⋄ ⍞←';' ⋄ ⍞←duration ⋄ ⍞←';' ⋄ ⍞←'1;'
        ⍞←'algorithm=base,faithful=yes,bits=1'
    }
    showResultsDfn←{
        duration←1↑⍵
        passes←¯1↑⍵
        ⎕←2 3,1↓⍸bits
        ⍞←'Passes:' ⋄ ⍞←passes ⋄ ⍞←'Time:' ⋄ ⍞←duration
        ⍞←'Avg:' ⋄ ⍞←duration÷passes ⋄ ⍞←'Limit:' ⋄ ⍞←n
        ⍞←'Count:' ⋄ ⍞←countPrimes ⋄ ⍞←'Valid:' ⋄ ⍞←validateResults
        ⎕←''
        1
    }
:EndClass
