@ECHO OFF
SETLOCAL ENABLEEXTENSIONS
SETLOCAL ENABLEDELAYEDEXPANSION

TITLE %0 %*

SET DIR=%~dp0

CALL :GETTIMEINFRACSECONDS BEGSECONDSTOTAL

SET NMAXDEFAULT=1000000
SET NMAX=!NMAXDEFAULT!

CALL :GETARGS %*

SET PRIMECOUNTS=4,25,168,1229,9592,78498,664579,5761455

CALL :DELETEVARS
CALL :GETSQRT
CALL :SIEVE
REM CALL :ENUMPRIMES
CALL :COUNTPRIMES
REM CALL :DELETEVARS
CALL :GETELAPSED BEGSECONDSTOTAL
ECHO PrimeFiles-bt2585;1;!WHOLES!.!MILLIS!;1;algorithm=base,faithful=no
GOTO :END

:SIEVE
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   ECHO %0
   SET COMPCOUNT=0
   FOR /L %%L IN (3,2,!SQRT!) DO (
      SET /P _DUMMY=%%L <NUL
      IF NOT DEFINED %%L (
         SET /A START=%%L * %%L
         SET /A STEP=%%L * 2
         FOR /L %%V IN (!START!,!STEP!,!NMAX!) DO SET %%V=~
         )
      )
   ECHO.
   CALL :GETELAPSED
   GOTO :EOF

:COUNTPRIMES
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   SET COMPCOUNT=0
   FOR /F "TOKENS=*" %%A IN ('SET ^| FIND "=~" ^| FIND /V /C ""') DO SET COMPCOUNT=%%A
   SET /A PRIMECOUNT=((NMAX + 1) / 2) - COMPCOUNT
   ECHO PRIMECOUNT=!PRIMECOUNT!
   CALL :GETELAPSED
   GOTO :EOF

:ENUMPRIMES
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   ECHO %0
   SET /P _DUMMY=2 <NUL
   FOR /L %%L IN (3,2,!NMAX!) DO (
      IF NOT DEFINED %%L SET /P _DUMMY=%%L <NUL
      )
   ECHO.
   CALL :GETELAPSED
   GOTO :EOF

:GETARGS
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   ECHO %0
   :GETARGSLOOP
   SET ARG=%1
   SET VAL=%2
   SHIFT
   SHIFT
   IF DEFINED ARG (
      IF /I "!ARG!" EQU "NMAX" (
         SET NMAX=!VAL!
         IF NOT DEFINED NMAX ECHO NMAX=n must be provided. Defaulting to !NMAXDEFAULT! & SET NMAX=!NMAXDEFAULT!
         FOR /F "TOKENS=* DELIMS=0123456789" %%A IN ("!NMAX!") DO (
            SET NONNUMBER=%%A
            )
         IF DEFINED NONNUMBER ECHO NMAX=n must be numeric. Defaulting to !NMAXDEFAULT! & SET NMAX=!NMAXDEFAULT!
         )
      IF /I "!ARG!" EQU "VHD" (
         SET VHD=!VAL!
         IF NOT DEFINED VHD SET VHD=N
         ECHO "!VHD!" | FIND /I "YT1" >NUL && SET VHD=Y
         )
      GOTO :GETARGSLOOP
      )
   CALL :GETELAPSED
   GOTO :EOF

:DELETEVARS
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   ECHO %0
   FOR /F "TOKENS=1 DELIMS==" %%A IN ('SET ^| FIND "=~"') DO IF DEFINED %%A SET %%A=
   CALL :GETELAPSED
   GOTO :EOF

:GETTIMEINFRACSECONDS
   SET T=!TIME!
   SET OUTVARNAME=%1
   SET T=!T::0=:!
   SET T=!T:.0=.!
   FOR /F "TOKENS=1-4 DELIMS=:.," %%W IN ("!T!") DO (
      SET /A %1=%%W * 360000 + %%X * 6000 + %%Y * 100 + %%Z
      )
   GOTO :EOF

:GETELAPSED
   CALL :GETTIMEINFRACSECONDS ENDSECONDS
   SET ARG1=%1
   IF DEFINED ARG1 SET BEGSECONDS=!%1!
   SET /A ELAPSED=ENDSECONDS - BEGSECONDS
   SET /A WHOLES=ELAPSED / 100
   SET MILLIS=!ELAPSED:~-2!0
   IF "!ELAPSED!" LSS "0" (
      SET /A ELAPSED=ELAPSED + (24 * 360000)
      )
   SET /A ELAPSEDH=ELAPSED / 360000
   SET /A ELAPSED=ELAPSED - (ELAPSEDH * 360000)
   SET /A ELAPSEDM=ELAPSED / 6000
   SET /A ELAPSED=ELAPSED - (ELAPSEDM * 6000)
   SET /A ELAPSEDS=ELAPSED / 100
   SET /A ELAPSED=ELAPSED - (ELAPSEDS * 100)
   SET /A ELAPSEDF=ELAPSED
   SET /A ELAPSEDH+=100
   SET /A ELAPSEDM+=100
   SET /A ELAPSEDS+=100
   SET /A ELAPSEDF+=100
   SET ELAPSEDH=!ELAPSEDH:~1!
   SET ELAPSEDM=!ELAPSEDM:~1!
   SET ELAPSEDS=!ELAPSEDS:~1!
   SET ELAPSEDF=!ELAPSEDF:~1!
   REM ECHO !ELAPSEDH!:!ELAPSEDM!:!ELAPSEDS!.!ELAPSEDF!
   GOTO :EOF

:GETSQRT
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   ECHO %0
   SET SQRT=1
   SET SQR=1
   SET SQRTFOUND=
   FOR /L %%L IN (1,1,46340) DO (
      IF NOT DEFINED SQRTFOUND (
         SET /A SQR=%%L * %%L
         IF !SQR! GTR !NMAX! (
            SET SQRTFOUND=1
            SET /A SQRT-=1
            ) ELSE (
            SET /A SQRT+=1
            )
         )
      )
   SET /A SQRT %% 2 | FIND "0" >NUL && SET /A SQRT-=1
   CALL :GETELAPSED
   GOTO :EOF

:END
