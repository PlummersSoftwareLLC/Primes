@ECHO OFF
SETLOCAL ENABLEEXTENSIONS
SETLOCAL ENABLEDELAYEDEXPANSION

REM
REM Author Brad Thone, 2022-04-17
REM
REM Additional co-tweaking with
REM   Brian Williams (http://iqspiral.com)
REM
REM Written on a lazy Easter weekend to prove CMD is capable
REM
REM primefiles.cmd NMAX=n [VHD=<Y|N>]
REM
REM If VHD is requested, you must run this script
REM with elevated privileges (as admin)
REM
REM Hardware: Intel I7-3770@3.40Ghz, 16G RAM, 250G SSD on SATA2
REM Completes NMAX=1,000,000 in about 3.5 minutes
REM
REM Creates two (2) folders (COMPS and PRIMES) and subfolders and writes
REM zero-byte files to them to keep track of Primes and nonPrimes
REM
REM The screen output is a bit much, but I wanted to display and time everything
REM I'll clean this up soon, and resubmit.

SET DIR=%~dp0

CALL :GETARGS %*

REM PRIMECOUNTS FOR 10, 100, 1000, ...
SET PRIMECOUNTS=4,25,168,1229,9592,78498,664579,5761455

SET TEMPDIR=.

IF /I "!VHD!" EQU "Y" (
   SET VHDFILE=!DIR!PRIMES.VHD
   SET VHDSIZE=768
   SET VHDDRIV=
   SET VHDFSYS=NTFS
   SET VHDLABEL=PRIMESVHD
   CALL :DELETEVDISK
   CALL :CREATEVDISK
   CALL :FINDVHDDRIV
   IF NOT DEFINED VHDDRIV ECHO Couldn't find VHD drive & GOTO :ABEND
   SET TEMPDIR=!VHDDRIV!:
   )
CALL :CREATEFOLDERS
CALL :DELETEFILES
CALL :GETSQRT
CALL :SIEVE
CALL :COUNTPRIMES
IF /I "!VHD!" EQU "Y" (
   CALL :DELETEVDISK
   ) ELSE (
   CALL :DELETEFILES PARALLEL
   )
GOTO :END

:SIEVE
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   ECHO SIEVE
   BREAK>!TEMPDIR!\PRIMES\2
   SET /P DUMMY=2 <NUL
   FOR /L %%L IN (3,2,!SQRT!) DO (
      IF NOT EXIST !TEMPDIR!\COMPS\%%L (
         SET /P DUMMY=%%L <NUL
         BREAK>!TEMPDIR!\PRIMES\%%L
         SET /A START=%%L * %%L
         SET /A STEP=%%L * 2
         FOR /L %%V IN (!START!,!STEP!,!NMAX!) DO IF NOT EXIST !TEMPDIR!\COMPS\%%V BREAK>!TEMPDIR!\COMPS\%%V
         )
      )
   SET /A SQRT+=2
   FOR /L %%L IN (!SQRT!,2,!NMAX!) DO IF NOT EXIST !TEMPDIR!\COMPS\%%L BREAK>!TEMPDIR!\PRIMES\%%L
   ECHO.
   CALL :GETELAPSED
   GOTO :EOF

:COUNTPRIMES
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   ECHO COUNTING PRIMES
   SET PRIMECOUNT=0
   FOR /F "TOKENS=1" %%Z IN ('DIR !TEMPDIR!\PRIMES ^| FIND "File(s)"') DO SET PRIMECOUNT=%%Z
   ECHO PRIMECOUNT=!PRIMECOUNT!
   CALL :GETELAPSED
   GOTO :EOF

:GETARGS
   SET ARG=%1
   SET VAL=%2
   SHIFT
   SHIFT
   IF DEFINED ARG (
      IF /I "!ARG!" EQU "NMAX" (
         SET NMAX=!VAL!
         IF NOT DEFINED NMAX ECHO NMAX=n must be provided & PAUSE & EXIT 1
         FOR /F "TOKENS=* DELIMS=0123456789" %%A IN ("!NMAX!") DO (
            SET NONNUMBER=%%A
            )
         IF DEFINED NONNUMBER ECHO NMAX=n must be numeric & PAUSE & EXIT 1
         )
      IF /I "!ARG!" EQU "VHD" (
         SET VHD=!VAL!
         IF NOT DEFINED VHD SET VHD=N
         ECHO "!VHD!" | FIND /I "YT1" >NUL && SET VHD=Y
         )
      GOTO :GETARGS
      )
   ECHO NMAX='!NMAX!'
   ECHO VHD='!VHD!'
   GOTO :EOF

:CREATEFOLDERS
   MD !TEMPDIR!\COMPS  >NUL 2>NUL
   MD !TEMPDIR!\PRIMES >NUL 2>NUL
   GOTO :EOF

:CREATEVDISK
   SET DPSCRIPT=CREATEVDISK.TXT
   ECHO CREATE VDISK FILE=!VHDFILE! MAXIMUM=!VHDSIZE!>!DPSCRIPT!
   ECHO SELECT VDISK FILE=!VHDFILE!>>!DPSCRIPT!
   ECHO ATTACH VDISK>>!DPSCRIPT!
   ECHO DETAIL VDISK>>!DPSCRIPT!
   ECHO CONVERT MBR>>!DPSCRIPT!
   ECHO CREATE PARTITION PRIMARY>>!DPSCRIPT!
   ECHO FORMAT FS=!VHDFSYS! UNIT=4K LABEL=!VHDLABEL! QUICK>>!DPSCRIPT!
   ECHO ASSIGN>>!DPSCRIPT!
   DISKPART /S !DPSCRIPT!
   DEL !DPSCRIPT!
   GOTO :EOF

:FINDVHDDRIV
   FOR %%Z IN (C D E F G H I J K L M N O P Q R S T U V W X Y Z) DO (
      VOL %%Z: 2>NUL | FIND "!VHDLABEL!" >NUL && SET VHDDRIV=%%Z
      )
   GOTO :EOF

:DELETEVDISK
   SET DPSCRIPT=DELETEVDISK.TXT
   ECHO SELECT VDISK FILE=!VHDFILE!>!DPSCRIPT!
   ECHO DETACH VDISK>>!DPSCRIPT!
   DISKPART /S !DPSCRIPT!
   DEL !DPSCRIPT!
   DEL !VHDFILE!
   GOTO :EOF

:DELETEFILES
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   ECHO DELETING FILES
   IF "%1" EQU "PARALLEL" (
      FOR /F "TOKENS=*" %%A IN ('DIR COMPS /B /ON /AD') DO START "!TEMPDIR!\COMPS\%%A" /MIN CMD /C DEL /Q !TEMPDIR!\COMPS\%%A\*
      ) ELSE (
      DEL /Q /S !TEMPDIR!\COMPS\* >NUL 2>NUL
      )
   DEL /Q /S !TEMPDIR!\PRIMES\* >NUL 2>NUL
   CALL :GETELAPSED
   GOTO :EOF

:GETTIMEINFRACSECONDS
   SET T=!TIME!
   SET OUTVARNAME=%1
   SET T=!T::0=:!
   SET T=!T:.0=.!
   FOR /F "TOKENS=1-4 DELIMS=:." %%W IN ("!T!") DO (
      SET /A %1=%%W * 360000 + %%X * 6000 + %%Y * 100 + %%Z
      )
   GOTO :EOF

:GETELAPSED
   CALL :GETTIMEINFRACSECONDS ENDSECONDS
   SET /A ELAPSED=ENDSECONDS - BEGSECONDS
   IF "!ELAPSED!" LSS "0" (
      SET /A ELAPSED=ELAPSED + (24 * 360000)
      )
   SET /A ELAPSEDH=ELAPSED / 360000
   SET /A ELAPSED=ELAPSED - (ELAPSEDH * 360000)
   SET /A ELAPSEDM=ELAPSED / 6000
   SET /A ELAPSED=ELAPSED - (ELAPSEDM * 6000)
   SET /A ELAPSEDS=ELAPSED / 100
   SET /A ELAPSED=ELAPSED - (ELAPSEDS * 100)
   SET /A ELAPSEDF=ELAPSED
   REM PREPEND ZEROES
   SET /A ELAPSEDH+=100
   SET /A ELAPSEDM+=100
   SET /A ELAPSEDS+=100
   SET /A ELAPSEDF+=100
   SET ELAPSEDH=!ELAPSEDH:~1!
   SET ELAPSEDM=!ELAPSEDM:~1!
   SET ELAPSEDS=!ELAPSEDS:~1!
   SET ELAPSEDF=!ELAPSEDF:~1!
   ECHO ELAPSED=!ELAPSEDH!:!ELAPSEDM!:!ELAPSEDS!.!ELAPSEDF! (!ENDSECONDS!0ms - !BEGSECONDS!0ms)
   GOTO :EOF

:GETSQRT
   CALL :GETTIMEINFRACSECONDS BEGSECONDS
   ECHO GET SQUARE ROOT OF !NMAX!
   SET SQRT=1
   SET SQR=1
   SET SQRTFOUND=
   FOR /L %%L IN (1,1,46340) DO (
      IF NOT DEFINED SQRTFOUND (
         SET /A SQR=%%L * %%L
         IF !SQR! GTR !NMAX! (
            SET SQRTFOUND=1
            SET /A SQRT-=1
            ) ELSE (
            SET /A SQRT+=1
            )
         )
      )
   ECHO !SQRT:~-1! | FINDSTR "0 2 4 6 8" >NUL && SET /A SQRT-=1
   ECHO SQUAREROOT=!SQRT!
   CALL :GETELAPSED
   GOTO :EOF

:ABEND
   PAUSE
   EXIT 1
   GOTO :EOF

:END
