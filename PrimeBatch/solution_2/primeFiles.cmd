@ECHO OFF
SETLOCAL ENABLEEXTENSIONS
SETLOCAL ENABLEDELAYEDEXPANSION

SET _DIR=%dp0

SET MAXLIMIT=%1
IF NOT DEFINED MAXLIMIT ECHO Need max number & GOTO :EOF

REM PRIMECOUNTS FOR 10, 100, 1000, ...
SET PRIMECOUNTS=4,25,168,1229,9592,78498,664579,5761455

FOR /L %%L IN (1,1,99) DO MD COMPS\%%L >NUL 2>NUL
FOR /L %%L IN (0,1,9) DO MD COMPS\0%%L >NUL 2>NUL
MD PRIMES >NUL 2>NUL

CALL :GETTIMEINFRACSECONDS !TIME! BEGSECONDS
ECHO DELETING FILES
DEL /Q /S COMPS\* >NUL 2>NUL
DEL /Q /S PRIMES\* >NUL 2>NUL
CALL :GETTIMEINFRACSECONDS !TIME! ENDSECONDS
CALL :GETELAPSED

CALL :GETTIMEINFRACSECONDS !TIME! BEGSECONDS
ECHO GET SQUARE ROOT OF !MAXLIMIT!
CALL :GETSQRT !MAXLIMIT! SQRT
CALL :GETTIMEINFRACSECONDS !TIME! ENDSECONDS
CALL :GETELAPSED

CALL :GETTIMEINFRACSECONDS !TIME! BEGSECONDS
ECHO SIEVE
CALL :SIEVE
CALL :GETTIMEINFRACSECONDS !TIME! ENDSECONDS
CALL :GETELAPSED

CALL :GETTIMEINFRACSECONDS !TIME! BEGSECONDS
ECHO COUNTING PRIMES
CALL :COUNTPRIMES
CALL :GETTIMEINFRACSECONDS !TIME! ENDSECONDS
CALL :GETELAPSED

CALL :GETTIMEINFRACSECONDS !TIME! BEGSECONDS
ECHO DELETING FILES
FOR /F "TOKENS=*" %%A IN ('DIR COMPS /B /ON /AD') DO START "COMPS\%%A" /MIN CMD /C DEL /Q COMPS\%%A\*
DEL /Q /S PRIMES\* >NUL 2>NUL
CALL :GETTIMEINFRACSECONDS !TIME! ENDSECONDS
CALL :GETELAPSED

GOTO :END

:SIEVE
   ECHO.>PRIMES\2
   FOR /L %%L IN (3,2,!SQRT!) DO (
      SET /P DUMMY=%%L <NUL
      SET L=%%L
      SET L2=!L:~-2!
      IF NOT EXIST COMPS\!L2!\%%L (
         ECHO.>PRIMES\%%L
         SET /A START=%%L * 2
         FOR /L %%V IN (!START!,%%L,!MAXLIMIT!) DO (
            SET V=%%V
            SET V2=!V:~-2!
            IF NOT EXIST COMPS\!V2!\%%V ECHO.>COMPS\!V2!\%%V
            )
         )
      )
   SET /A SQRT+=2
   FOR /L %%L IN (!SQRT!,2,!MAXLIMIT!) DO SET L=%%L& SET L2=!L:~-2!& IF NOT EXIST COMPS\!L2!\%%L ECHO.>PRIMES\%%L
   ECHO.
   REM SET PRIME-
   GOTO :EOF

:COUNTPRIMES
   SET PRIMECOUNT=0
   FOR /F "TOKENS=*" %%Z IN ('DIR PRIMES /B') DO SET /A PRIMECOUNT+=1
   ECHO PRIMECOUNT=!PRIMECOUNT!
   GOTO :EOF

:GETTIMEINFRACSECONDS
   ECHO %1
   SET OUTVARNAME=%2
   SET T=%1
   SET T=!T::0=:!
   SET T=!T:.0=.!
   FOR /F "TOKENS=1-4 DELIMS=:." %%W IN ("!T!") DO (
      SET /A %2=%%W * 360000 + %%X * 6000 + %%Y * 100 + %%Z
      )
   GOTO :EOF

:GETELAPSED
   SET /A ELAPSED=ENDSECONDS - BEGSECONDS
   IF "!ELAPSED!" LSS "0" (
      SET /A ELAPSED=ELAPSED + (24 * 360000)
      )
   SET /A ELAPSEDH=ELAPSED / 360000
   SET /A ELAPSED=ELAPSED - (ELAPSEDH * 360000)
   SET /A ELAPSEDM=ELAPSED / 6000
   SET /A ELAPSED=ELAPSED - (ELAPSEDM * 6000)
   SET /A ELAPSEDS=ELAPSED / 100
   SET /A ELAPSED=ELAPSED - (ELAPSEDS * 100)
   SET /A ELAPSEDF=ELAPSED
   REM PREPEND ZEROES
   SET /A ELAPSEDH+=100
   SET /A ELAPSEDM+=100
   SET /A ELAPSEDS+=100
   SET /A ELAPSEDF+=100
   SET ELAPSEDH=!ELAPSEDH:~1!
   SET ELAPSEDM=!ELAPSEDM:~1!
   SET ELAPSEDS=!ELAPSEDS:~1!
   SET ELAPSEDF=!ELAPSEDF:~1!
   ECHO ELAPSED=!ELAPSEDH!:!ELAPSEDM!:!ELAPSEDS!.!ELAPSEDF! (!ENDSECONDS!0ms - !BEGSECONDS!0ms)
   GOTO :EOF

:GETSQRT
   SET ROOT=1
   SET SQR=1
   SET ROOTFOUND=
   FOR /L %%L IN (1,1,46340) DO (
      IF NOT DEFINED ROOTFOUND (
         SET /A SQR=%%L * %%L
         IF !SQR! GTR %1 (
            SET ROOTFOUND=1
            SET /A ROOT-=1
            ) ELSE (
            SET /A ROOT+=1
            )
         )
      )
   ECHO !ROOT:~-1! | FINDSTR "0 2 4 6 8" >NUL && SET /A ROOT-=1
   SET %2=!ROOT!
   ECHO SQUAREROOT=!ROOT!
   GOTO :EOF

:END
